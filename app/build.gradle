apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "androidx.navigation.safeargs.kotlin"

def retrofitVersion = "2.4.0"
def rxJavaVersion = "2.2.3"
def rxAndroidVersion = "2.0.1"
def appCompatVersion = "1.0.2"
def coordinatorVersion = "1.0.0"
def lifecycleVersion = "2.1.0"
def constraintLayoutVersion = "1.1.3"
def junitVersion = "5.5.1"
def mockkVersion = "1.9.3.kotlin12"
def assertJVersion = "3.11.1"
def moshiVersion = "1.8.0"
def glideVersion = "4.8.0"
def roomVersion = "1.1.1"
def daggerVersion = "2.22.1"
def assistedInjectVersion = "0.5.0"
def savedStateVersion = "1.0.0-alpha01"
def navigationVersion = "2.1.0"
def timberVersion = "4.7.1"
def coreVersion = "1.0.1"
def fragmentVersion = "1.1.0"
def epoxyVersion = "3.8.0"

def compileSDKVersion = 28
def minSDKVersion = 21
def targetSDKVersion = 28

def appVersionCode = 1
def appVersionName = "1.0"

android {
    compileSdkVersion compileSDKVersion
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "com.lebedevsd.githubviewer"
        minSdkVersion minSDKVersion
        targetSdkVersion targetSDKVersion
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release_key")
            storePassword 'password'
            keyAlias 'github'
            keyPassword 'password'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            debuggable true
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kapt {
    useBuildCache = true
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$savedStateVersion"

    // Epoxy
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    implementation "com.airbnb.android:epoxy-paging:$epoxyVersion"
    implementation "com.airbnb.android:epoxy-databinding:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    // Logger
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Used to fetch images from URLs
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Networking library for android
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    // Serialisation library for android
    implementation "com.squareup.moshi:moshi:$moshiVersion"

    // rxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // DI library
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Support library for creating factory of factories for ViewModels
    implementation "com.squareup.inject:assisted-inject-annotations-dagger2:$assistedInjectVersion"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:$assistedInjectVersion"

    // testing libraries
    testImplementation "android.arch.persistence.room:testing:$roomVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "junit:junit:4.12"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junitVersion"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
}
